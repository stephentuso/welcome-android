apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion sdkVersion.compile
    buildToolsVersion buildToolsVer

    if (!deploying) {
        publishNonDefault true
    }

    defaultConfig {
        minSdkVersion sdkVersion.min
        targetSdkVersion sdkVersion.target
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            testCoverageEnabled true
        }
    }

    sourceSets {
        main.res.srcDirs += 'src/main/res-public'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    compile support.annotations
    compile support.v4
    compile support.appcompat
    compile support.preference
}

apply from: '../gradle/bintray.gradle'

task welcomeBintrayUpload(type: GradleBuild) {
    tasks = ['compileDebugSources', 'generatePomFileForMavenJavaPublication', 'publishToMavenLocal', 'bintrayUpload']
    startParameter.projectProperties = [deploying: true]
}
preBuild.mustRunAfter welcomeBintrayUpload

configure(welcomeBintrayUpload) {
    group = 'Publishing'
    description = 'Upload task for welcome-android'
}

task generatePublicXml(type: Exec) {
    workingDir 'src/main/res-public'
    commandLine 'bash', '-c', 'export PATH="$PATH:/usr/local/bin"; pubxml'
    ignoreExitValue true
}

// Don't run pubxml task on Windows
if (! System.getProperty('os.name').toLowerCase().contains('windows')) {
    preBuild.dependsOn generatePublicXml
}
