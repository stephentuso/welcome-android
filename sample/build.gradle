plugins {
    alias(libs.plugins.agp.app)
}

def versionProps = rootProject.file('version.properties').withInputStream { stream ->
    new Properties().tap { load(stream) }
}

android {
    namespace = "com.stephentuso.welcomeexample"
    compileSdk = versionProps['targetSdk'].toInteger()

    defaultConfig { 
        minSdk = versionProps['minSdk'].toInteger()
        targetSdk = compileSdk
        versionCode = 4
        versionName = versionProps['versionName'] + ".0"

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
            signingConfig = signingConfigs.debug
        }
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles += 'proguard-rules.pro'
        }
    }

    packaging {
        jniLibs.keepDebugSymbols.add("**/libandroidx.graphics.path.so")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
}

dependencies {
    implementation project(":library")
    implementation(libs.androidx.preference)

    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.test.runner)
    androidTestImplementation(libs.androidx.test.rules)
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}
